plugins{
	id 'java'
	id 'java-library'
	id 'maven-publish'
	id 'eclipse'
	id 'signing'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
			exclude '**/*.dll'
		}
		resources{
			srcDirs = ['src']
			exclude '**/*.java'
		}
	}
}

java{
	withSourcesJar()
	withJavadocJar()
}

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8

group = 'dev.roanh.util'
version = System.getenv("VERSION") ? System.getenv("VERSION").substring(1) : '2.3-SNAPSHOT'

publishing{
	publications{
		mavenJava(MavenPublication){
			artifactId = 'util'
			from components.java
		
			pom{
				name = 'Util'
				description = 'Utilities library for public projects'
				url = 'https://github.com/RoanH/Util'
				licenses{
					license{
						name = 'GNU General Public License v3.0'
						url = 'http://www.gnu.org/licenses/gpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan@roanh.dev'
					}
				}
				scm{
					connection = 'scm:git:git@github.com:RoanH/Util.git'
					developerConnection = 'scm:git:git@git.roanh.dev:roan/util.git'
					url = 'https://github.com/RoanH/Util'
				}
			}
		}
	}
	
	repositories{
		maven{
			url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials{
				username findProperty("OSSRH_USER")
				password findProperty("OSSRH_PASS")
			}
		}
	}
	
	jar{
		into("META-INF/maven/$project.group/$project.publishing.publications.mavenJava.artifactId"){
			from{
				generatePomFileForMavenJavaPublication
			}
			rename ".*", "pom.xml"
		}
	}
}

signing{
	useInMemoryPgpKeys(findProperty("signingKeyId"), findProperty("signingKey"), findProperty("signingPassword"))
	sign publishing.publications.mavenJava
}