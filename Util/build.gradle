plugins{
	id 'java-library'
	id 'maven-publish'
	id 'eclipse'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
	}
}

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8

group = 'com.github.RoanH'
version = '1.0-SNAPSHOT'

publishing{
	publications{
		nexus(MavenPublication){
			artifactId = 'Util'
			from components.java
		
			pom{
				name = 'Util'
				description = 'Utilities library for public projects'
				url = 'https://github.com/RoanH/Util'
				licenses{
					license{
						name = 'GNU General Public License v3.0'
						url = 'http://www.gnu.org/licenses/gpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan.hofland@hotmail.com'
					}
				}
				scm{
					connection = 'scm:git:git@github.com:RoanH/Util.git'
					developerConnection = 'scm:git:git@github.com:RoanH/Util.git'
					url = 'https://github.com/RoanH/Util'
				}
			}
		}
	}
	
	repositories{
		if(project.hasProperty("mavenPassword")){
			maven{
				url version.endsWith('SNAPSHOT') ? "$nexusSnapshots" : "$nexusReleases"
				credentials{
					username "$mavenUser"
					password "$mavenPassword"
				}
			}
		}
	}
}